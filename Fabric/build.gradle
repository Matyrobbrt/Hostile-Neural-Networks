plugins {
    id 'fabric-loom' version '1.1.+'
    id 'maven-publish'
    id 'idea'
}

archivesBaseName = "${mod_name}-fabric-${minecraft_version}"

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}+${minecraft_version}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    compileOnly project(":Common")

    include modApi('teamreborn:energy:3.0.0') {
        exclude(group: 'net.fabricmc.fabric-api')
    }

    modImplementation 'shadows.placebo:placebo-1.20.1-fabric:1.0.0'
    implementation 'io.github.matyrobbrt:eventdispatcher:1.8.0'

    modCompileOnlyApi("mezz.jei:jei-${minecraft_version}-common-api:${jei_version}")
    modCompileOnlyApi("mezz.jei:jei-${minecraft_version}-fabric-api:${jei_version}")
    // at runtime, use the full JEI jar for Fabric
    modRuntimeOnly("mezz.jei:jei-${minecraft_version}-fabric:${jei_version}")
//    modRuntimeOnly "curse.maven:techreborn-233564:4608596"
//    modRuntimeOnly "curse.maven:reborncore-237903:4608597"
    modRuntimeOnly "curse.maven:powah-rearchitected-633483:4622660"
    modRuntimeOnly "curse.maven:architectury-api-419699:4581904"
    modRuntimeOnly "curse.maven:cloth-config-348521:4573053"
}

loom {
    mixin {
        add('main', 'hostilenetworks.refmap.json') {
            include 'hostilenetworks.mixins.json'
            include 'hostilenetworks_fabric.mixins.json'
        }
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
    accessWidenerPath.set(file('../Common/src/main/resources/hostilenetworks.accesswidener'))
}


processResources {
    from project(":Common").sourceSets.main.resources
    filesMatching("${mod_id}.mixins.json") {
        expand "refmap_target": "${archivesBaseName}-"
    }
}

tasks.withType(JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            groupId = 'shadows.placebo'
            artifactId = "placebo-${minecraft_version}-fabric"
            version = project.version
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}
